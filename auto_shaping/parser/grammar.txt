?start: requirements_list | requirement


?requirements_list : "[" [req_or_ass ("," req_or_ass)*] "]"

?req_or_ass: requirement | assignment
?requirement: safety_requirement | target_requirement | comfort_requirement
?assignment: var "=" const

?safety_requirement: "ensure" expr -> ensure
?target_requirement: "achieve" expr -> achieve
                   | "conquer" expr -> conquer
?comfort_requirement: "encourage" expr -> encourage

?expr: fvar ">" signed_const    -> gt
     | fvar ">=" signed_const   -> ge
     | fvar "<" signed_const    -> lt
     | fvar "<=" signed_const   -> le

?fvar: var -> var
     | "abs" var -> abs
     | "exp" var -> exp

?var: ESCAPED_STRING

?signed_const: const | "+" const -> pos | "-" const -> neg
?const: NUMBER -> number | ESCAPED_STRING -> string

%import common.ESCAPED_STRING
%import common.WS_INLINE
%import common.NUMBER
%ignore WS_INLINE